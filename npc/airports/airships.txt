//===== rAthena Script ======================================= 
//= The Airship System Script
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= The Airship System used in the official servers.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

//============================================================ 
//= Domestic Airship Route
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================ 

airplane,243,73,0	script	#AeroplanoPortal-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch_:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AeroplanoPortal-1)	#AeroplanoPortal-2	45,1,1

airplane,1,1,0	script	Aeroplano_Domestico	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","Nós estamos partindo para Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","Em breve chegaremos a Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Einbroch. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","Atualmente nós estamos em Einbroch. O Aeroplano irá decolar em instantes.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","Nós estamos partindo para Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","Em breve chegaremos a Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Lighthalzen. Tenha uma boa viagem.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","Atualmente nós estamos em Lighthalzen. O Aeroplano irá decolar em instantes.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","Nós estamos partindo para Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","Em breve chegaremos a Einbroch.",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Einbroch. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","Atualmente nós estamos em Einbroch. O Aeroplano irá decolar em instantes.",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Juno.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","Nós estamos partindo para Juno.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","Em breve chegaremos a Juno.",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Juno. Tenha uma boa viagem.",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","Atualmente nós estamos em Juno. O Aeroplano irá decolar em instantes.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","Nós estamos partindo para Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","Em breve chegaremos a Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Hugel. Tenha uma boa viagem.",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","Atualmente nós estamos em Hugel. O Aeroplano irá decolar em instantes.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Juno.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","Nós estamos partindo para Juno.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","Em breve chegaremos a Juno.",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Juno. Tenha uma boa viagem.",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","Atualmente nós estamos em Juno. O Aeroplano irá decolar em instantes.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Einbroch.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Saída#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Saída#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Saída#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Saída#airplane2b	857

airplane,100,69,3	script	Tripulação do Aeroplano#ein-1	852,{
	mes "[Tripulação do Aeroplano]";
	mes "Se já pousamos no";
	mes "seu destino e você";
	mes "gostaria de sair do Aeroplano,";
	mes "por favor use as";
	mes "escadas a frente. Obrigado";
	mes "por sua ajuda.";
	close;
}

airplane,64,94,1	script	Criança de Umbala#ein_p	787,{
	emotion e_swt2;
	mes "[Criança]";
	if (event_umbala >= 3) {
		mes "Uau, mãe!";
		mes "V-veja isso!";
		mes "Nós estamos voando! N-nós estamos...";
		mes "Estamos conhecendo o céu!";
	}
	else {
		mes "Makumalagu!";
		mes "Saampa joojimbo";
		mes "kaku na jedi Solo.";
		mes "Bwahahahahahahaah!";
	}
	close;
}

airplane,66,93,3	script	Senhora de Umbala#ein_p	783,{
	emotion e_dots;
	mes "[Senhora]";
	if (event_umbala >= 3) {
		mes "Shush...";
		mes "Querido, comporte-se~";
		mes "Não se anime tanto";
		mes "enquanto não sairmos";
		mes "deste lugar público!";
	}
	else {
		mes "Chooktu!";
		mes "Sacraup matii!";
		mes "Shaka gurftalfi";
		mes "huntiki manjoo!";
	}
	close;
}

airplane,71,91,7	script	Homem de Umbala#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[Chrmlim]";
		mes "Ei você~";
		mes "Pela sua aparência,";
		mes "percebo que você";
		mes "consegue me entender.";
		mes "^333333*Legal...!*^000000";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Tenho ajudado a";
		mes "equipe do Aeroplano";
		mes "auxiliando seus Tripulantes";
		mes "que têm medo de altura";
		mes "os levanto para pular de bungee jumping em Umbala. Legal, em?";
		next;
		mes "[Chrmlim]";
		mes "Mas...";
		mes "Alguns deles não podem";
		mes "superar seus medos.";
		mes "Devem participar de";
		mes "algumas experiências, ^333333em Nifflheim^000000.";
	}
	else {
		mes "[Chrmlim]";
		mes "Bajoo ga";
		mes "nukta Airship.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Shabala moow bajama";
		mes "Airship kulaha googoona ";
		mes "salu. Dama, kookoo na nu";
		mes "yukuta. Um, fashuku na ret!";
	}
	close;
}

airplane,250,58,2	script	Equipe do Aeroplano#airplane	67,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Assistente do Aeroplano]";
		mes "Bem-vind"+(Sex?"o":"a")+"";
		mes "ao Aeroplano.";
		mes "Como posso ajudá-lo?";
		next;
		select("Você tem um passageiro chamado Thierry?");
		mes "[Assistente do Aeroplano]";
		mes "Me desculpe, mas eu não conheço nenhum dos passageiros pelo nome.";
		close;
	}
	// Hugel quest end
	mes "[Assistente do Aeroplano]";
	mes "Bem-vind"+(Sex?"o":"a")+"";
	mes "ao Aeroplano.";
	mes "Como posso lhe ajudar?";
	next;
	switch (select("Embarcar no Aeroplano:Cabine do Capitão:Atrativos:Cancelar")) {
	case 1:
		mes "[Assistente do Aeroplano]";
		mes "Quando você ver o aviso";
		mes "anunciando que chegamos";
		mes "no seu destino desejado,";
		mes "por favor, use uma das saídas";
		mes "localizadas nas partes";
		mes "norte e sul do Aeroplano.";
		next;
		mes "[Assistente do Aeroplano]";
		mes "Caso você perca o momento";
		mes "de sua parada, não se preocupe.";
		mes "O Aeroplano está constantemente";
		mes "em uma rota única e você";
		mes "terá outra chance para descer";
		mes "no destino exato que quiser.";
		close;
	case 2:
		mes "[Assistente do Aeroplano]";
		mes "A Cabine do Capitão";
		mes "fica na parte frontal";
		mes "do Aeroplano. Lá, você pode";
		mes "conhecer o capitão e";
		mes "o piloto do Aeroplano.";
		close;
	case 3:
		mes "[Assistente do Aeroplano]";
		mes "O Aeroplano possui vários";
		mes "mini-jogos para o";
		mes "entretenimento de todos";
		mes "os passageiros. Você está convidad"+(Sex?"o":"a")+"";
		mes "a testar sua sorte";
		mes "no Mini-Jogos do Aeroplano~";
		close;
	case 4:
		mes "[Assistente do Aeroplano]";
		mes "Bem, desejo-lhe uma boa";
		mes "estadia durante o vôo";
		mes "em nosso Aeroplano. Obrigado";
		mes "e tenha um bom dia.";
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	834,{
	mes "[Zerta]";
	mes "Ah, olá aventureiro.";
	mes "Eu estou atualmente em";
	mes "uma jornada sacrada, oferecendo";
	mes "sacrifícios por todo o continente";
	mes "de Rune-Midgard.";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	714,{
	mes "[Maelin]";
	mes "Hum, o Aeroplano";
	mes "vai até Lutie, não é? Eu tenho";
	mes "esperado durante um longo tempo,";
	mes "mas até agora não tenho";
	mes "tido notícias de Lutie.";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	702,{
	mes "[Aanos]";
	mes "Ah uau~";
	mes "O céu parece estar";
	mes "tão diferente";
	mes "e lindo hoje!";
	close;
}

airplane,221,158,2	script	Piloto#airplane	852,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Piloto]";
		mes "Espero que o drink esteja fresco e saboroso.";
		mes "Bebidas são a minha vida! Beber me dá energias!";
		mes "Eu não vivo sem bebidas!";
		next;
		mes "[Piloto]";
		mes "Mas! Dirigir sob influência da bebida não é bom.";
		mes "Porém! Eu preciso estar bebendo mais e mais!";
		emotion e_sob;
		next;
		select("Do you know a passenger named Thierry?");
		mes "[Piloto]";
		mes "Este uniforme é";
		mes "realmente bonito, mas";
		mes "não gosto de usá-lo enquanto";
		mes "estou no Aeroplano.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Ninguém nunca realmente";
		mes "entra neste quarto.";
		mes "E como sou o capitão. Eu poderia simplesmente ficar de cuecas.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Ah!? O que é isto!";
		next;
		mes "- ...Ele não está ouvindo tudo que você fala. -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[Piloto]";
		mes "Faz bastante tempo";
		mes "desde que eu esperimentei";
		mes "um bom e gelado álcool.";
		mes "Mas a profissão";
		mes "requer que eu esteja";
		mes "sempre sóbrio!";
		next;
		mes "[Piloto]";
		mes "Sempre beba com responsabilidade!";
		mes "Ainda sim, não me lembro";
		mes "a última vez que";
		mes "entrei de férias. Sim, uma bebida,";
		mes "uma TV e tudo";
		mes "ficará ótimo.";
		emotion e_sob;
		close;
	case 2:
		mes "[Piloto]";
		mes "Cara, o clima";
		mes "está ótimo hoje.";
		mes "Brilho, o céu está aberto";
		mes "com uma boa visibilidade";
		mes "e segurança, um voo despreocupado.";
		close;
	case 3:
		mes "[Piloto]";
		mes "Você sabe, seu capitão é";
		mes "um homem respeitado. Ele e seu";
		mes "irmão são atualmente os";
		mes "mais conhecidos na indústria aérea.";
		mes "Quem diria que nos tornariamos";
		mes "capitões de voo tão bons?";
		next;
		mes "[Piloto]";
		mes "Apenas entre nós,";
		mes "tenho que dizer a você,";
		mes "o Papai Noel foi para algum lugar";
		mes "com renas e elfos";
		mes "que trabalham para ele.";
		mes "Aquele cara é um gênio!";
		close;
	default:
		mes "[Piloto]";
		mes "Você sabe, toda essa coisa";
		mes "de pilotagem no ar,";
		mes "é algo bastante recente, você sabe não é?";
		mes "Sim, temos um Aeroplano voando";
		mes "a uma incrível velocidade.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Ainda assim, há sempre algo sério,";
		mes "realmente acontecendo.";
		mes "Quer dizer, estavamos treinando";
		mes "enquanto o Aeroplano";
		mes "estava sendo inventado.";
		mes "Não é uma loucura?!";
		close;
	}
}

airplane,50,66,5	script	Vendedor de Maçãs#airplane	86,{
	mes "[Fruitz]";
	mes "Bem-vindo a Banca de Frutas";
	mes "onde você pode comprar Maçãs";
	mes "ou juntar ingredientes para";
	mes "fazer o Suco de Maçã.";
	next;
	switch (select("Comprar Maçãs.:Fazer Suco de Maçã.:Por que você está aqui?:Cancelar.")) {
	case 1:
		mes "[Fruitz]";
		mes "Por favor me diga a quantidade";
		mes "de Maçãs que você deseja comprar.";
		mes "Cada Maçã custa 15 zeny";
		mes "e você pode comprar no máximo";
		mes "500 por vez. Digite '0' para";
		mes "cancelar sua compra.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Fruitz]";
				mes "Obrigado por visitar minha banca.";
				mes "Adeus!";
				mes "Venha quando você";
				mes "sentir que precisa";
				mes "de uma Maçã.";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Fruitz]";
				mes "Você escolheu uma quantia";
				mes "muito baixa ou muito alta,";
				mes "o valor mínimo é 1 e o máximo são 500.";
				mes "Por favor digite o número de";
				mes "maçãs que você deseja comprar.";
				next;
			}
			else {
				mes "[Fruitz]";
				mes "Um total de ^FF0000" + .@input + "^000000 Maçãs";
				mes "irá custar a você ^FF0000" + .@pay + " Zeny^000000 zeny.";
				mes "Você gostaria de continuar?";
				next;
				if (select("Sim:Não") == 2) {
					mes "[Fruitz]";
					mes "Obrigado por visitar minha banca.";
					mes "Adeus!";
					mes "Venha quando você";
					mes "sentir que precisa";
					mes "de uma Maçã.";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Fruitz]";
			mes "Me desculpe, mas você não possui";
			mes "a quantidade de zenys necessária";
			mes "para comprar essas Maçãs.";
			mes "Por favor verifique a quantidade de zenys";
			mes "que você possui e quantas Maçãs quer comprar.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Fruitz]";
			mes "Humm, eu acho que você não pode carregar mais itens";
			mes "em seu inventário, assim não pode comprar mais Maçãs.";
			mes "Você poderia liberar um pouco";
			mes "de espaço em seu inventário?";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Fruitz]";
			mes "Obrigado por visitar minha banca.";
			mes "Adeus! Venha quando você";
			mes "sentir que precisa de uma Maçã ~!";
			close;
		}
	case 2:
		mes "[Fruitz]";
		mes "Tudo bem, irei precisar de";
		mes "^FF00003 Maçãs e 1 Garrafa Vazia^000000";
		mes "para criar 1 Suco de Maçã para você.";
		mes "Você deseja continuar?";
		next;
		switch (select("Sim:Não")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Fruitz]";
				mes "Me desculpe,mas você não possui";
				mes "os materiais suficientes para criar";
				mes "uma Garrafa de Suco de Maçã.";
				mes "Lembre-se, irei precisar de";
				mes "3 Maçãs e 1 Garrafa Vazia.";
				close;
			}
			else {
				mes "[Fruitz]";
				mes "Obrigado,";
				mes "por favor espere";
				mes "um momento.";
				next;
				mes "^3355FF*Moer moer*";
				mes "*Moer moer*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Maçã
				delitem 713,1; //Garrafa Vazia
				getitem 531,1; //Suco de Maçã
				mes "[Fruitz]";
				mes "Aqui está~";
				mes "Espero que você goste!";
				mes "Por favor fique a vontade";
				mes "para comprar Maçãs ou fazer";
				mes "Sucos de Maçãs";
				mes "quando você precisar, aventureiro~";
				close;
			}
		case 2:
			mes "[Fruitz]";
			mes "Obrigado por visitar minha banca.";
			mes "Adeus!";
			mes "Venha quando você";
			mes "sentir que precisa";
			mes "de uma Maçã ~.";
			close;
		}
	case 3:
		mes "[Fruitz]";
		mes "Eu costumava ser um andarilho";
		mes "vagabundo, até que um dia,";
		mes "tirei uma soneca e algo";
		mes "atingiu minha cabeça e acordei";
		mes "do meu sono tranquilo.";
		next;
		mes "[Fruitz]";
		mes "Acontece que eu estava dormindo";
		mes "embaixo de uma Macieira";
		mes "e foi quando uma maçã caiu";
		mes "e atingiu minha cabeça.";
		mes "Eu estava morrendo de fome e";
		mes "estava prestes a comer aquela Maçã...";
		next;
		mes "[Fruitz]";
		mes "Mas de repente, Kain, meu antigo";
		mes "amigo desde os tempos de mineração,";
		mes "me pediu para ajudá-lo aqui";
		mes "no Aeroplano. Então eu aceitei,";
		mes "e foi lá que encontrei pessoas";
		mes "jogando um Jogo de Dados.";
		next;
		mes "[Fruitz]";
		mes "Estava entediado e curioso";
		mes "e acabei apostando algumas";
		mes "simples Maçãs no jogo dos dados.";
		mes "Mas por alguma razão, eu";
		mes "tive uma incrível sorte.";
		mes "Uma maçã se multiplicou em duas... ";
		next;
		mes "[Fruitz]";
		mes "Duas tornaram-se quatro e";
		mes "antes que eu percebesse, eu";
		mes "já tinha feito uma banca de Maçãs!";
		mes "Ganhei tantas maçãs, e eu";
		mes "acabei começando meu próprio negócio";
		mes "aqui no Aeroplano. Estranho, não acha?";
		next;
		mes "[Fruitz]";
		mes "Maçãs são boas para você.";
		mes "Elas são concerteza";
		mes "muito boas para mim.";
		mes "Hahahahaah~!";
		close;
	case 4:
		mes "[Fruitz]";
		mes "Obrigado por utilizar";
		mes "a minha loja.";
		mes "Tchau~";
		close;
	}
}

//============================================================ 
//= International Airship Route
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AeroplanoPortal-3	45,1,1,{
	end;
	
OnTouch_:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AeroplanoPortal-3)	#AeroplanoPortal-4	45,1,1

airplane_01,1,1,0	script	Aeroplano_Internacional	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","Nós estamos partindo para Izlude.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","Em breve chegaremos a Izlude.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation2,1;
	donpcevent "#AeroplanoPortal-3::OnUnhide";
	donpcevent "#AeroplanoPortal-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo a Izlude. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","Atualmente nós estamos em Izlude. O Aeroplano irá decolar em instantes.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AeroplanoPortal-3::OnHide";
	donpcevent "#AeroplanoPortal-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando agora. Seu próximo destino é Juno.",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","Nós estamos partindo para Juno.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","Em breve chegaremos a Juno.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation2,2;
	donpcevent "#AeroplanoPortal-3::OnUnhide";
	donpcevent "#AeroplanoPortal-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo a Juno. Tenha uma boa viagem.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","Atualmente nós estamos em Juno. O Aeroplano irá decolar em instantes.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AeroplanoPortal-3::OnHide";
	donpcevent "#AeroplanoPortal-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando agora. Seu próximo destino é Rachel.",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","Nós estamos partindo para Rachel.",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","Em breve chegaremos a Rachel.",bc_map,"0xFF8200";
	end;
OnTimer220000:
	set $@airplanelocation2,0;
	donpcevent "#AeroplanoPortal-3::OnUnhide";
	donpcevent "#AeroplanoPortal-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo a Rachel. Tenha uma boa viagem.",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","Atualmente nós estamos em Rachel. O Aeroplano irá decolar em instantes.",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AeroplanoPortal-3::OnHide";
	donpcevent "#AeroplanoPortal-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando agora. Seu próximo destino é Izlude.",bc_map,"0x00ff00";
	stopnpctimer;
	set .moninv, .moninv + 1;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		set .moninv, 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Saída#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Saída#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	Saída#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	Saída#airplane_012b	857

airplane_01,250,58,2	script	Equipe do Aeroplano#airplane01	67,{
	mes "[Assistente do Aeroplano]";
	mes "Bem-vind"+(Sex?"o":"a")+" ";
	mes "ao Aeroplano.";
	mes "Como posso lhe ajudar?";
	next;
	switch (select("Embarcar no Aeroplano:Cabine do Capitão:Atrativos:Cancelar")) {
	case 1:
		mes "[Assistente do Aeroplano]";
		mes "Quando você ver o aviso";
		mes "anunciando que chegamos no seu destino";
		mes "desejado, por favor,";
		mes "use uma das saídas localizadas";
		mes "nas partes norte e";
		mes "sul do Aeroplano.";
		next;
		mes "[Assistente do Aeroplano]";
		mes "Caso você perca o momento de";
		mes "sua parada, não se preocupe.";
		mes "O Aeroplano está constantemente";
		mes "em uma rota única e você terá";
		mes "outra chance para descer no";
		mes "destino exato que quiser.";
		close;
	case 2:
		mes "[Assistente do Aeroplano]";
		mes "A Cabine do Capitão fica na";
		mes "parte frontal do Aeroplano.";
		mes "Lá, você pode";
		mes "conhecer o capitão e o";
		mes "piloto do Aeroplano.";
		close;
	case 3:
		mes "[Assistente do Aeroplano]";
		mes "O Aeroplano possui vários";
		mes "mini-jogos para o entretenimento";
		mes "de todos os passageiros.";
		mes "Você está convidad"+(Sex?"o":"a")+" ";
		mes "a testar sua sorte no";
		mes "Mini-Jogos do Aeroplano~";
		close;
	case 4:
		mes "[Assistente do Aeroplano]";
		mes "Bem, desejo-lhe uma boa estadia";
		mes "durante o vôo";
		mes "em nosso Aeroplano.";
		mes "Obrigado e tenha um bom dia.";
		close;
	}
}

airplane_01,50,66,5	script	Vendedor de Maçãs#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto !! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor volte novamente -";
		mes "- após perder um pouco de seu peso. -";
		close;
	}
	mes "[Meltz]";
	mes "Bem-vindo a Banca de Frutas";
	mes "onde você pode comprar Maçãs";
	mes "ou juntar ingredientes para";
	mes "fazer o Suco de Maçã.";
	next;
	switch (select("Comprar Maçãs.:Fazer Suco de Maçã.:Cancelar.")) {
	case 1:
		mes "[Meltz]";
		mes "Por favor me diga a quantidade";
		mes "de Maçãs que você deseja comprar.";
		mes "Cada Maçã custa 15 zeny";
		mes "e você pode comprar no máximo";
		mes "500 por vez. Digite '0' para";
		mes "cancelar sua compra.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "Obrigado por visitar minha banca.";
				mes "Adeus!";
				mes "Venha quando você";
				mes "sentir que precisa";
				mes "de uma Maçã.~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Meltz]";
				mes "Você escolheu uma quantia";
				mes "muito alta, o valor";
				mes "máximo são 500. Por favor";
				mes "digite o número de";
				mes "maçãs que você deseja comprar.";
				next;
			}
			else {
				mes "[Meltz]";
				mes "Um total de ^FF0000" + .@input + "^000000 Maçãs";
				mes "irá custar a você ^FF0000" + .@pay + "^000000 zeny.";
				mes "Você gostaria de continuar?";
				next;
				if (select("Sim:Não") == 2) {
					mes "[Meltz]";
					mes "Obrigado por visitar minha banca.";
					mes "Adeus!";
					mes "Venha quando você";
					mes "sentir que precisa";
					mes "de uma Maçã.~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "Me desculpe, mas você não possui";
			mes "a quantidade de zenys necessária para comprar essas Maçãs.";
			mes "Por favor verifique a quantidade de zenys";
			mes "que você possui e quantas Maçãs quer comprar.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "Humm, eu acho que você não pode carregar mais itens";
			mes "em seu inventário, assim não pode comprar mais Maçãs.";
			mes "Você poderia liberar um pouco";
			mes "de espaço em seu";
			mes "inventário?";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "Obrigado por visitar minha banca.";
			mes "Adeus! Venha quando você";
			mes "sentir que precisa de uma Maçã ~!";
			close;
		}
	case 2:
		mes "[Meltz]";
		mes "Tudo bem, irei precisar de";
		mes "^FF00003 Maçãs e 1 Garrafa Vazia^000000";
		mes "para criar 1 Suco de Maçã para você.";
		mes "Você deseja continuar?";
		next;
		switch (select("Sim:Não")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Meltz]";
				mes "Me desculpe,mas você não possui";
				mes "os materiais suficientes para criar";
				mes "uma Garrafa de Suco de Maçã.";
				mes "Lembre-se, irei precisar de";
				mes "3 Maçãs e 1 Garrafa Vazia.";
				close;
			}
			else {
				mes "[Meltz]";
				mes "Obrigado, por favor espere um momento.";
				next;
				mes "^3355FF*Moer* *Moer*";
				mes "*Moer* *Moer*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Maçã
				delitem 713,1; //Garrafa Vazia
				getitem 531,1; //Suco de Maçã
				mes "[Meltz]";
				mes "Aqui está~";
				mes "Por favor, volte sempre.";
				close;
			}
		case 2:
			mes "[Meltz]";
			mes "Obrigado por visitar minha banca.";
			mes "Adeus!";
			mes "Venha quando você";
			mes "sentir que";
			mes "precisa de uma Maçã. ~";
			close;
		}
	case 3:
		mes "[Meltz]";
		mes "Obrigado por visitar minha banca.";
		mes "Adeus!";
		mes "Venha quando você";
		mes "sentir que";
		mes "precisa de uma Maçã.~";
		close;
	}
}

airplane_01,221,158,2	script	Piloto#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[Piloto]";
		mes "Longitude, 131 degrees east.";
		mes "Latitude, 37 degrees north.";
		mes "We're right on course, captain.";
		close;
	case 2:
		mes "[Piloto]";
		mes "Looks like a really";
		mes "cloudy day. Always hard";
		mes "to navigate when the skies";
		mes "aren't clear. Guess we'll";
		mes "need to amp the radar.";
		close;
	case 3:
		mes "[Piloto]";
		mes "The Captain is a good";
		mes "man and I can't think of";
		mes "a finer person to command";
		mes "this ship. Still, he's pretty";
		mes "tough, a real slave driver.";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000Hey...!^000000";
		mes "^ff0000Less chit-chat^000000";
		mes "^ff0000and more piloting!^000000";
		next;
		mes "[Piloto]";
		mes "R-right away, sir!";
		mes "(See what I mean?)";
		close;
	default:
		mes "[Piloto]";
		mes "This uniform is";
		mes "really dapper, but";
		mes "it's way too thick to";
		mes "wear around the Airship.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "No one ever really";
		mes "comes into this room.";
		mes "And the captain IS a reindeer.";
		mes "I could just strip to my boxers.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Oh...! Hello there!";
		mes "E-e-enjoying your flight?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[Dianne]";
	mes "It's so weird!";
	mes "I went to visit the";
	mes "Airship Captain and";
	mes "all I saw was this";
	mes "weird reindeer. Oh!";
	mes "Do you think that...";
	close;

OnTouch_:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[Mendel]";
	mes "As I expected, the";
	mes "in-flight meals are";
	mes "three star quality at best.";
	mes "*Harrrumph* I really should";
	mes "have brought my chef so that";
	mes "I could enjoy a real meal.";
	close;
}

airplane_01,71,31,2	script	Swordsman Shimizu#air_01	106,{
	mes "[Swordsman Shimizu]";
	mes "Finally, after five";
	mes "years of waiting...";
	mes "I can have my revenge!";
	next;
	mes "[Swordsman Shimizu]";
	mes "I just...";
	mes "Have to make sure that";
	mes "I don't keep missing my";
	mes "stop. But soon, very soon,";
	mes "vengeance will be mine!";
	close;
}

//============================================================ 
//= Typing Challenge
//============================================================ 
airplane_01,32,61,4	script	Nils#ein	49,1,1,{
	mes "[Nils]";
	mes "Welcome to the";
	mes "^ff0000RO Typing Challenge^000000.";
	mes "Would you like to play";
	mes "a quick typing game?";
	next;
	switch (select("Play ^ff0000RO Typing Challenge^000000:Information:View Top Records:Cancel")) {
	case 1:
		mes "[Nils]";
		mes "Okay, we have";
		mes "a new challenger!";
		mes "Enter the following";
		mes "text as quickly as you";
		mes "can without making any";
		mes "mistakes! Let's start~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		set .@wordtest, rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save1$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save2$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Nils]";
			mes "Your record is ^ff0000" + .@total_time + " seconds^000000 and";
			mes "the total letters are " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Nils]";
				mes "Hmmm, this record isn't";
				mes "humanly possible unless you";
				mes "copy and paste the whole";
				mes "sentence. Please play fairly";
				mes "next time.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Nils]";
				mes "The previous top record was";
				mes "made by ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "with the total ^0000ff" + $050320_ein_typing + "^000000 letters.";
				mes "However, ^ff0000" + strcharinfo(0) + "^000000,";
				mes "you made the new top record";
				mes "this time. Congratulations!";
				set $050320_minus1_typing$, strcharinfo(0);
				set $050320_ein_typing, .@tasoo;
				close;
			}
			else {
				mes "[Nils]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "is the current";
				mes "record holder with";
				mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
				mes "characters. Try to beat";
				mes "that record next time~";
				close;
			}
		}
		else {
			mes "[Nils]";
			mes "Oooh...";
			mes "I'm sorry, but";
			mes "you entered the";
			mes "text incorrectly...";
			close;
		}
	case 2:
		mes "[Nils]";
		mes "The ^ff0000RO Typing Challenge^000000";
		mes "is a game where you enter";
		mes "the given text as quickly as you";
		mes "can. The name of the top player";
		mes "is recorded for posterity. If you";
		mes "want fame, here's your chance!";
		next;
		mes "[Nils]";
		mes "I'd just like to let";
		mes "you know that you type";
		mes "all the text that you see";
		mes "in the single input line that";
		mes "you're given. So don't press";
		mes "the enter key, just click 'OK.'";
		close;
	case 3:
		mes "[Nils]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "is the current";
		mes "record holder with";
		mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
		mes "characters. Try to beat";
		mes "that record next time~";
		close;
	case 4:
		mes "[Nils]";
		mes "Feel free to take on the";
		mes "^ff0000RO Typing Challenge^000000";
		mes "anytime. I'll be here~";
		close;
	}
}

//============================================================ 
//= Apple Gambling
//============================================================ 
airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "Hi, I'm Clarice~";
	mes "How would you like";
	mes "to wager some Apples";
	mes "in a friendly game of Dice?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

function	script	applegamble	{
	.@npc_name$ = getarg(0);
	switch (select("Play Dice Game:Learn Dice Game Rules:Cancel")) {
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "I'm up for a game of";
		mes "dice whenever you feel";
		mes "like it. Just talk to me if";
		mes "you ever get hit with the";
		mes "sudden urge to gamble, kay?";
		close;
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "The rules for the Dice game";
		mes "are pretty simple. First, you";
		mes "place a bet by wagering Apples.";
		mes "You can bet a maximum of 50";
		mes "Apples at a time. To keep things";
		mes "legal, I can only accept Apples.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "But hey, if all that zeny";
		mes "is burning a hole in your";
		mes "pocket, head over to Fruitz";
		mes "and you can buy as many";
		mes "Apples as you want, playah~";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Now, we begin with me";
		mes "rolling two 6-sided dice.";
		mes "When it's your turn, you'll";
		mes "roll two 6-sided dice. After";
		mes "that, both of us will have the";
		mes "option of rolling a third die.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Now here's the important";
		mes "thing. If your total is higher";
		mes "than 12, you'll bust, meaning";
		mes "that you lose. Otherwise, the";
		mes "person with the higher total";
		mes "is the winner. Got it?";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Now, you'll be the first";
		mes "to decide whether or not";
		mes "you'll roll the third die. Then,";
		mes "depending on your result, I'll";
		mes "roll my third die... Or maybe not.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "When you win, you'll";
		mes "receive twice as many";
		mes "Apples as you wagered.";
		mes "But if we happen to tie, you";
		mes "get the Apples that you bet";
		mes "returned to you. Fair, right?";
		close;
	case 1:
		break;
	}
	mes "["+ .@npc_name$ +"]";
	mes "Ooh, so you'll play with";
	mes "me? Great! How many";
	mes "Apples would you like to bet?";
	mes "Remember, you can wager";
	mes "up to 50 Apples. If you'd like";
	mes "to cancel, please enter '0'.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "Changed your mind?";
			mes "I understand. Well then,";
			mes "I hope we can play sometime.";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+ .@npc_name$ +"]";
			mes "You can't bet more than";
			mes "50 Apples. Remember, we";
			mes "need to keep these stakes";
			mes "reasonable. Please enter";
			mes "a value no greater than 50.";
			next;
			continue;
		}
		mes "["+ .@npc_name$ +"]";
		mes "So you'll be";
		mes "betting ^FF0000"+.@amount+"^000000 Apples.";
		mes "Is that right?";
		next;
		if (select("Yes:No") == 2) {
			mes "["+ .@npc_name$ +"]";
			mes "Mm, made a mistake?";
			mes "Alright, please enter the";
			mes "number of Apples you";
			mes "wish to place in this bet";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "I'm sorry, but you";
			mes "don't seem to have";
			mes "enough Apples for this";
			mes "bet... You can't gamble";
			mes "if you can't play, you know.";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+ .@npc_name$ +"]";
		mes "Good!";
		mes "Now we can start";
		mes "this game! I'll roll first~";
		break;
	}
	mes "^3355FF*Rolling and rumbling*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+ .@npc_name$ +"]";
	mes "I got a ^0000FF" + .@table1 + "^000000 and a ^0000FF" + .@table2 + "^000000.";
	mes "That's a total of ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, now it's your turn.";
	next;
	select("Cast Dice.");
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+ .@npc_name$ +"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, you have ^FF0000" + .@player1 + "^000000 and ^FF0000" + .@player2 + "^000000. The total is ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+ .@npc_name$ +"]";
	if(.@playersub == .@tablesub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. We are making an even game. Would you like to cast dice again?";
	} else if (.@playersub > .@tablesub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, you are currently winning this game. Would you like to cast dice again?";
	} else if(.@tablesub > .@playersub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. I am winning this game. Would you like to cast dice again?";
	}
	next;
	switch (select("Cast dice.:Cancel.")) {
	case 1:
		mes "^3355FF*Rolling and rumbling*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+ .@npc_name$ +"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. You lost this game. I am sorry but please try again.";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. Even though you casted dice again, still your total is smaller than mine. You lost the game. I am sorry and please try again.";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. I don't want to take any risk, let's end this game in a draw. Let's play again some other time~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "Alright.";
			mes "Let me cast the dice again.";
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. Now it is my turn.";
		}
		break;
	case 2:
		mes "["+ .@npc_name$ +"]";
		if (.@playersub > .@tablesub) {
			mes "I see, you don't want to take risk of losing the game. Okay, let me cast dice again.";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "I see, you don't want to take risk of losing this game. Neither do I, let's end this game in a draw. Let's play again some other time~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "Alright.";
			mes "Let me cast the dice again.";
		}
		else {
			mes "It couldn't hurt to try.";
			mes "Well, I win this time.";
			mes "I'm sorry, let's try play";
			mes "again sometime.";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+ .@npc_name$ +"]";
	if (.@tabletotal > 12) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. I lost this game since my total exceeded 12. Let me give you my apples. Congratulations, that was a great game.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 you won this game, ^FF0000" + strcharinfo(0) + "^000000. Let me give you my apples. It was a great game and I hope we will play again some other time.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 this game came out even, ^FF0000" + strcharinfo(0) + "^000000. Let me give you your apple back. It was a great game and I hope we will play again some other time.";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 you lost this game, ^FF0000" + strcharinfo(0) + "^000000. I am sorry but please try again.";
		close;
	}
}
